apply plugin: 'com.android.application'

android {
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    compileSdkVersion 29
    buildToolsVersion "29.0.2"
    defaultConfig {
        applicationId "com.skysoft.smart.intranetchat"
        minSdkVersion 23
        targetSdkVersion 29
        versionCode gitVersionCode()
        versionName gitVersionTag()
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                cppFlags "-std=c++14"
            }
        }
    }
    signingConfigs {
        release {
            storeFile file(PLATFORM_STORE_FILE)
            keyAlias PLATFORM_KEY_ALIAS
            keyPassword PLATFORM_KEY_PASSWORD
            storePassword PLATFORM_STORE_PASSWORD
        }
        debug {
            storeFile file(PLATFORM_STORE_FILE)
            keyAlias PLATFORM_KEY_ALIAS
            keyPassword PLATFORM_KEY_PASSWORD
            storePassword PLATFORM_STORE_PASSWORD
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    applicationVariants.all {
        variant ->
            variant.outputs.all {
                Calendar calendar = Calendar.getInstance(Locale.CHINA);
                def buildDate = String.format(Locale.CHINA, "%04d%02d%02d", calendar.get(Calendar.YEAR), calendar.get(Calendar.MONTH) + 1, calendar.get(Calendar.DAY_OF_MONTH))
                def versionName = defaultConfig.versionName
                def versionCode = defaultConfig.versionCode
                if (variant.buildType.name.equals('release')) {
                    outputFileName = "${project.archivesBaseName}-v${versionName}-c${versionCode}-${buildDate}-release.apk"
                } else {
                    outputFileName = "${project.archivesBaseName}-v${versionName}-c${versionCode}-${buildDate}-debug.apk"
                }
            }
    }

    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
            version "3.10.2"
        }
    }

    sourceSets {
        main {
            java.srcDirs = ["src/main/java", "src/main/aidl"]
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'org.greenrobot:eventbus:3.1.1'

    implementation 'com.github.tbruyelle:rxpermissions:0.10.2'
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.annotation:annotation:1.1.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.1.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    implementation 'com.squareup.okhttp3:okhttp:3.12.0'
    implementation 'com.squareup.retrofit2:retrofit:2.6.2'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava:2.6.2'
    implementation 'com.jakewharton:butterknife:10.2.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:10.2.0'
    implementation 'androidx.room:room-runtime:2.2.0'
    annotationProcessor 'androidx.room:room-compiler:2.2.0'
    implementation 'de.hdodenhof:circleimageview:3.0.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.8'
    implementation 'com.ashokvarma.android:bottom-navigation-bar:2.0.3'
    implementation 'com.github.bumptech.glide:glide:4.4.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.4.0'
    implementation 'jp.wasabeef:glide-transformations:2.0.2'
    implementation 'androidx.exifinterface:exifinterface:1.1.0-rc01'
    implementation 'top.zibin:Luban:1.1.8'
}

static def gitVersionCode() {
    def cmd = 'git rev-list HEAD --first-parent --count'
    try {
        cmd.execute().text.trim().toInteger()
    } catch (Exception e) {
        return 0
    }
}

static def gitVersionTag() {
    def cmd = 'git describe --tags'
    def version = cmd.execute().text.trim()
    if (version.isEmpty()) {
        version = "1.0"
    }
    return version + '.' + gitVersionCode()
}
